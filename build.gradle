plugins {
    id 'fi.jasoft.plugin.vaadin' version '1.0' apply false
}

allprojects {
  group = 'org.vaadin.addons'
  version = project.hasProperty('BUILD_VERSION') ? getProperty('BUILD_VERSION') : 'development';
  apply plugin: 'eclipse-wtp'
}

subprojects {
  apply plugin: 'fi.jasoft.plugin.vaadin'
  vaadin {
      version = '7.7.3'
  }
  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  javadoc.enabled = false
  vaadinJavadocJar.enabled = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

/**
 * Vaadin addon project
 */
project(':addon'){
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'osgi'

  configurations {
      deploy
  }

  dependencies {
    testCompile 'junit:junit:4.8.+'
    deploy      'org.apache.maven.wagon:wagon-ssh:2.2'
  }

  vaadin {
      addon {
          author 'John Ahlroos'
          license 'Apache 2.0'
          title 'DragDropLayouts'
      }
  }

  task createAddonPom {
    pom{ artifactId = 'dragdroplayouts' }.writeTo("${sourceSets.main.output.resourcesDir}/META-INF/maven/fi.jasoft/dragdroplayouts/pom.xml")
  }

  jar {
    baseName = 'dragdroplayouts'
    dependsOn test, createAddonPom

    // Include sources
    sourceSets.main.java.srcDirs.each{
      from it
    }

    manifest {
      // the manifest of the default jar is of type OsgiManifest
      name = 'dragdroplayouts'
      /*
       it seems we need to export the client-side stuff too to make it easier to include the jar file as project dependency
       instruction 'Export-Package', '!fi.jasoft.dragdroplayouts.client.*', '*'
       */
      instruction 'Import-Package', '!com.google.gwt.*', '!com.vaadin.client.*', '*'
      instruction 'Bundle-Vendor', 'johndevs'
      instruction 'Bundle-Description', 'Drag and drop layouts addon'
      instruction 'Bundle-DocURL', 'https://github.com/johndevs/dragdroplayouts'
    }
  }

  // required for Windows build due to gradle vaadin plugin bug
  task fixWidgetSetCssRef(dependsOn: vaadinUpdateWidgetset) {
      doLast {
          def wsFile = project.file('src/main/resources/fi/jasoft/dragdroplayouts/DragDropLayoutsWidgetSet.gwt.xml')

          def fixedXml = wsFile.text.replace("fi_jasoft_dragdroplayouts\\", "fi_jasoft_dragdroplayouts/")
          wsFile.write(fixedXml)
      }
  }

  processResources.dependsOn fixWidgetSetCssRef

  vaadinSourcesJar {
      baseName = 'dragdroplayouts'
  }

  vaadinJavadocJar {
      baseName = 'dragdroplayouts'
  }


  artifacts {
      archives jar
  }

  uploadArchives {
      dependsOn createAddonPom
      repositories.mavenDeployer {
          def snapshotRepositoryUrl = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY')
          def snapshotRepositoryUser = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_USER')
          def snapshotRepositoryPassword = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_PASSWORD')
          def snapshotRepositoryPrivateKey = System.getProperty('PLUGIN_MAVEN_SNAPSHOT_REPOSITORY_KEY')

          configuration = configurations.deploy

          snapshotRepository( url: snapshotRepositoryUrl ) {
             authentication(
                     userName: snapshotRepositoryUser,
                     password: snapshotRepositoryPassword,
                     privateKey: snapshotRepositoryPrivateKey
             )
          }

          addFilter('jar') {artifact, file ->
              artifact.ext == 'jar'
          }
      }
  }

  configurations.archives.artifacts.removeAll {
      it.file =~ 'war' || it.file =~ 'javadoc'
  }
}

/*
 * Demo application for demonstrating the addon
 */
project(':demo'){
  apply plugin: 'java'

  dependencies {
    compile project(':addon')
    compile group:'de.sven-jacobs', name:'loremipsum', version:'1.0'
    compile group:'de.java2html',   name:'java2html',  version:'5.0'
  }

  vaadinRun {
    serverPort 7676
    debug true
    classesDir 'bin'
  }

  war {
    // WAR filename
    archiveName = 'DragDropLayouts7.war'

    // Include widgetset
    dependsOn 'vaadinCompile'
    dependsOn 'test' 

    // Add sources
    webInf{
      into('classes'){
        from sourceSets.main.allJava
       }
    }
  }

  test {
    dependsOn 'vaadinCompile'
    systemProperties = System.properties
  }
}